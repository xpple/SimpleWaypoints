import dev.xpple.simplewaypoints.buildscript.GenerateBuildInfoTask

plugins {
    id 'fabric-loom' version "${fabric_loom_version}"
    id 'maven-publish'
    id 'signing'
    id 'dev.lukebemish.central-portal-publishing' version "${central_portal_publishing_version}"
}

version = project.mod_version
group = project.maven_group
description = project.mod_description

base {
    archivesName = project.archives_base_name
}

loom {
    accessWidenerPath = file('src/main/resources/simplewaypoints.aw')
}

repositories {
    mavenCentral()
    maven {
        name = 'xpple'
        url = 'https://maven.xpple.dev/maven2'
    }
    maven {
        name = 'ParchmentMC'
        url = 'https://maven.parchmentmc.org'
    }
    maven {
        name = 'DJtheRedstoner'
        url = 'https://pkgs.dev.azure.com/djtheredstoner/DevAuth/_packaging/public/maven/v1'
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.layered {
        officialMojangMappings {
            nameSyntheticMembers = true
        }
        parchment "org.parchmentmc.data:${project.parchment_mappings}"
    }
    modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"

    include modImplementation("dev.xpple:clientarguments:${project.clientarguments_version}")
    include modImplementation("dev.xpple:betterconfig-fabric:${project.betterconfig_version}")

    modRuntimeOnly("me.djtheredstoner:DevAuth-fabric:${project.devauth_version}") {
        exclude group: 'net.fabricmc', module: 'fabric-loader'
    }

    testImplementation platform("org.junit:junit-bom:${project.junit_version}")
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.register('generateBuildInfo', GenerateBuildInfoTask) {
    outputFile = new File(temporaryDir, "build_info.json")
}

processResources {
    def props = [
        version: project.mod_version,
        mod_description: project.mod_description,
        minecraft_version_dependency: project.minecraft_version_dependency,
        loader_version: project.fabric_loader_version,
    ]
    inputs.properties props
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand props
    }

    from("LICENSE") {
        rename {"${it}_${project.base.archivesName.get()}"}
    }

    from generateBuildInfo
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21

    withSourcesJar()
    withJavadocJar()
}

javadoc {
    options.addBooleanOption('html5', true)
}

test {
    useJUnitPlatform()
}

centralPortalPublishing.bundle('simplewaypoints') {
    username = System.getenv("PORTAL_USER")
    password = System.getenv("PORTAL_TOKEN")

    publishingType = "USER_MANAGED"
    // publishingType = "AUTOMATIC"
}

tasks.publish.dependsOn tasks.named("publishSimplewaypointsCentralPortalBundle")

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = project.base.archivesName.get()
            from components.java

            pom {
                name = project.name
                description = project.mod_description
                url = 'https://github.com/xpple/SimpleWaypoints'
                licenses {
                    license {
                        name = 'GNU Lesser General Public License, Version 3'
                        url = 'https://www.gnu.org/licenses/lgpl-3.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'xpple'
                        name = 'Frederik van der Els'
                        email = 'fred@xpple.dev'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/xpple/SimpleWaypoints.git'
                    developerConnection = 'scm:git:ssh://github.com/xpple/SimpleWaypoints.git'
                    url = 'https://github.com/xpple/SimpleWaypoints'
                }
            }
        }
    }

    repositories {
        centralPortalPublishing.portalBundle(':', 'simplewaypoints')
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/xpple/SimpleWaypoints"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
        maven {
            name = "xpple"
            url = "sftp://xpple.dev:22/maven.xpple.dev/httpdocs/maven2"
            credentials {
                username = System.getenv("MAVEN_USER")
                password = System.getenv("MAVEN_PASS")
            }
        }
    }
}

signing {
    // only register task on GitHub Actions
    if (System.getenv("GITHUB_ACTIONS") != "true") {
        logger.lifecycle("Disabled artifact signing!")
        return
    }
    def keyData = [
        signingKeyId: findProperty("signingKeyId"),
        signingKey: findProperty("signingKey"),
        signingPassword: findProperty("signingPassword"),
    ]
    def missing = keyData.findAll { it.value == null }
    if (!missing.isEmpty()) {
        logger.lifecycle("Missing in memory PGP key (missing ${missing.keySet().join(", ")}), disabled artifact signing!")
        return
    }
    useInMemoryPgpKeys(keyData.signingKeyId, keyData.signingKey, keyData.signingPassword)
    sign publishing.publications.mavenJava
    logger.lifecycle("Enabled artifact signing!")
}
